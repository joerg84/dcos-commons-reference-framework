ext {
  aws = "http://downloads.mesosphere.io/"
  basePath = "kafka/assets/"
  packageVer = "stub-universe" // is replaced
  cliBase = "dcos-kafka-"
}

task packageVersion (
  group: "universe",
  description: "Provides the release version for the package")  << {
  packageVer = "git describe --tags".execute().text.trim()
  println packageVer
}

task universe(
  dependsOn: packageVersion,
  group: "universe",
  description: "Copies universe files to build and modifies there artifact dir") {
  inputs.file file("universe/package")
  // incremental builds might be nice but need to research multiple inputs
  // outputs.dir "$buildDir/universe"
  doLast {
    // supports kafka 4 digit version 0.9.0.1
    if( !((packageVer ==~ /^\d+\.\d+\.\d+\-\d+\.\d+\.\d+/) ||
    (packageVer ==~ /^\d+\.\d+\.\d+\-\d+\.\d+\.\d+\.\d+/)))
      throw new GradleException("Invalid Release Version: $packageVer")

    copy {
      from "universe/package"
      into "$buildDir/universe"
      filter{
        String line -> line.replaceAll("\\{\\{artifact-dir\\}\\}", aws + basePath + packageVer)
      }
      filter{
        String line -> line.replaceAll("stub-universe", packageVer)
      }
      // future proof the client
      filter{
        String line -> line.replaceAll("dcos-kafka-0.2.5.tar.gz", cliBase + cliVersion + ".tar.gz")
      }
    }
  }
}

task universeDist(type: Zip, dependsOn: universe,
  group: "universe",
  description: "Creates a zip of the universe") {
  from "$buildDir/universe"
  archiveName = "universe.zip"
  destinationDir = file("$buildDir/universe/")
}

task releaseTag(group: "universe",
  description: "Executes a git tag using the projectVersion-serviceVersion") << {
  if (project.hasProperty('serviceVersion')) {
    println "tagging version: $version-$serviceVersion"
    println "git tag $version-$serviceVersion".execute().err.text
  } else {
    println "serviceVersion is NOT defined"
  }
}
